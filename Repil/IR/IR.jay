%{
using System;
using System.Collections.Generic;
using System.IO;
using System.Numerics;
using System.Text;
using System.Linq;

using Repil.Types;

#pragma warning disable 219,414

namespace Repil.IR
{
	public partial class Parser
	{
%}

%token INTEGER HEX_INTEGER FLOAT_LITERAL STRING TRUE FALSE UNDEF VOID NULL LABEL X
%token SOURCE_FILENAME TARGET DATALAYOUT TRIPLE
%token GLOBAL_SYMBOL LOCAL_SYMBOL META_SYMBOL META_SYMBOL_DEF SYMBOL DISTINCT METADATA CONSTANT_BYTES
%token TYPE HALF FLOAT DOUBLE I1 I8 I16 I32 I64 ZEROINITIALIZER
%token DEFINE DECLARE UNNAMED_ADDR LOCAL_UNNAMED_ADDR NOALIAS ELLIPSIS GLOBAL
%token CONSTANT PRIVATE INTERNAL EXTERNAL FASTCC
%token NONNULL NOCAPTURE WRITEONLY READONLY
%token ATTRIBUTE_GROUP_REF ATTRIBUTES NORECURSE NOUNWIND READNONE SPECULATABLE SSP UWTABLE ARGMEMONLY
%token RET BR SWITCH INDIRECTBR INVOKE RESUME CATCHSWITCH CATCHRET CLEANUPRET UNREACHABLE
%token FNEG
%token ADD NUW NSW FADD SUB FSUB MUL FMUL UDIV SDIV FDIV UREM SREM FREM
%token SHL LSHR EXACT ASHR
%token AND OR XOR
%token EXTRACTELEMENT INSERTELEMENT SHUFFLEVECTOR
%token EXTRACTVALUE INSERTVALUE
%token ALLOCA LOAD STORE FENCE CMPXCHG ATOMICRMW GETELEMENTPTR ALIGN INBOUNDS INRANGE
%token TRUNC ZEXT SEXT FPTRUNC FPEXT TO
%token FPTOUI FPTOSI UITOFP SITOFP PTRTOINT INTTOPTR BITCAST ADDRSPACECAST
%token ICMP EQ NE UGT UGE ULT ULE SGT SGE SLT SLE
%token FCMP OEQ OGT OGE OLT OLE ONE ORD UEQ UNE UNO
%token PHI SELECT CALL TAIL VA_ARG
%token LANDINGPAD CATCHPAD CLEANUPPAD

%start module

%%

module
	: module_parts
	;
	
module_parts
    : module_part
    | module_parts module_part
    ;
    
module_part
    : SOURCE_FILENAME '=' STRING
    {
        module.SourceFilename = (string)$3;
    }
    | TARGET DATALAYOUT '=' STRING
    {
        module.TargetDatalayout = (string)$4;
    }
    | TARGET TRIPLE '=' STRING
    {
        module.TargetTriple = (string)$4;
    }
    | LOCAL_SYMBOL '=' TYPE literal_structure
    {
        module.IdentifiedStructures[(Symbol)$1] = (StructureType)$4;
    }
    | function_definition
    {
        var f = (FunctionDefinition)$1;
        module.FunctionDefinitions[f.Symbol] = f;
    }
    | function_declaration
    {
        var f = (FunctionDeclaration)$1;
        module.FunctionDeclarations[f.Symbol] = f;
    }
    | global_variable
    {
        var g = (GlobalVariable)$1;
        module.GlobalVariables[g.Symbol] = g;
    }
    | ATTRIBUTES ATTRIBUTE_GROUP_REF '=' '{' attributes '}'
    | META_SYMBOL_DEF '=' '!' '{' '}'
    {
        module.Metadata[(Symbol)$1] = new List<object> (0);
    }
    | META_SYMBOL_DEF '=' '!' '{' metadata '}'
    {
        module.Metadata[(Symbol)$1] = $5;
    }
    | META_SYMBOL_DEF '=' META_SYMBOL '(' metadata_args ')'
    {
        var m = SymsAdd ($5, Symbol.Intern("_f"), $3);
        module.Metadata[(Symbol)$1] = m;
    }
    | META_SYMBOL_DEF '=' DISTINCT '!' '{' metadata '}'
    {
        module.Metadata[(Symbol)$1] = $6;
    }
    | META_SYMBOL_DEF '=' DISTINCT META_SYMBOL '(' metadata_args ')'
    {
        var m = SymsAdd ($6, Symbol.Intern("_f"), $4);
        module.Metadata[(Symbol)$1] = m;
    }
    ;
    
global_variable
    : GLOBAL_SYMBOL '=' function_addr global_kind type constant ',' ALIGN INTEGER metadata_kvs
    {
        $$ = new GlobalVariable ((GlobalSymbol)$1, (bool)$4, (LType)$5, (Constant)$6, isPrivate: false);
    }
    | GLOBAL_SYMBOL '=' visibility function_addr global_kind type constant ',' ALIGN INTEGER
    {
        $$ = new GlobalVariable ((GlobalSymbol)$1, (bool)$5, (LType)$6, (Constant)$7, isPrivate: true);
    }
    | GLOBAL_SYMBOL '=' linkage function_addr global_kind type ',' ALIGN INTEGER
    {
        $$ = new GlobalVariable ((GlobalSymbol)$1, (bool)$5, (LType)$6, null, isPrivate: true);
    }
    ;
    
global_kind
    : GLOBAL { $$ = true; }
    | CONSTANT { $$ = true; }
    ;
    
linkage
    : EXTERNAL
    | INTERNAL
    ;
    
visibility
    : PRIVATE
    ;
    
metadata_args
    : metadata_arg
    {
        var t = (Tuple<object, object>)$1;
        $$ = NewSyms (t.Item1, t.Item2);
    }
    | metadata_args ',' metadata_arg
    {
        var t = (Tuple<object, object>)$3;
        $$ = SymsAdd ($1, t.Item1, t.Item2);
    }
    ;
    
metadata_arg
    : SYMBOL ':' SYMBOL       { $$ = Tuple.Create ($1, $3); }
    | SYMBOL ':' META_SYMBOL  { $$ = Tuple.Create ($1, $3); }
    | SYMBOL ':' STRING       { $$ = Tuple.Create ($1, $3); }
    | SYMBOL ':' constant     { $$ = Tuple.Create ($1, $3); }
    | TYPE ':' META_SYMBOL    { $$ = Tuple.Create ($1, $3); }
    | SYMBOL ':' META_SYMBOL '(' metadata_value_args ')'
    {
        $$ = Tuple.Create ($1, $3);
    }
    | SYMBOL ':' META_SYMBOL '(' ')'
    {
        $$ = Tuple.Create ($1, $3);
    }
    ;
    
metadata_kvs
    : META_SYMBOL META_SYMBOL
    {
        $$ = NewSyms ($1, (MetaSymbol)$2);
    }
    | metadata_kvs META_SYMBOL META_SYMBOL
    {
        $$ = SymsAdd ($1, $2, (MetaSymbol)$3);
    }
    ;
    
metadata
    : metadatum
    {
        $$ = NewList ($1);
    }
    | metadata META_SYMBOL
    {
        $$ = ListAdd ($1, $2);
    }
    | metadata ',' typed_value
    {
        $$ = ListAdd ($1, $3);
    }
    | metadata ',' META_SYMBOL
    {
        $$ = ListAdd ($1, $3);
    }
    | metadata ',' NULL
    {
        $$ = ListAdd ($1, $3);
    }
    ;
    
metadatum
    : typed_value
    | META_SYMBOL
    | NULL
    ;
    
attributes
    : attribute
    | attributes attribute
    ;
    
attribute
    : NORECURSE
    | NOUNWIND
    | READNONE
    | SPECULATABLE
    | SSP
    | UWTABLE
    | ARGMEMONLY
    | STRING '=' STRING
    | STRING
    | SYMBOL
    | READONLY
    | SYMBOL '(' metadata_value_args ')'
    ;
    
literal_structure
    : '{' type_list '}'
    {
        var s = new LiteralStructureType ((List<LType>)$2);
        $$ = s;
    }
    ;
    
type_list
    : type
    {
        $$ = NewList ((LType)$1);
    }
    | type_list ',' type
    {
        $$ = ListAdd ($1, (LType)$3);
    }
    ;
    
return_type
    : type
    | VOID { $$ = VoidType.Void; }
    ;
    
type
    : literal_structure
    | HALF   { $$ = FloatType.Half; }
    | FLOAT  { $$ = FloatType.Float; }
    | DOUBLE { $$ = FloatType.Double; }
    | I1     { $$ = IntegerType.I1; }
    | I8     { $$ = IntegerType.I8; }
    | I16    { $$ = IntegerType.I16; }
    | I32    { $$ = IntegerType.I32; }
    | I64    { $$ = IntegerType.I64; }
    | return_type '(' function_type_args ')'
    {
        $$ = new FunctionType ((LType)$1, (List<LType>)$3);
    }
    | type '*'
    {
        $$ = new PointerType ((LType)$1, 0);
    }
    | LOCAL_SYMBOL
    {
        $$ = new NamedType ((Symbol)$1);
    }
    | '<' INTEGER X type '>'
    {
        $$ = new VectorType ((int)(BigInteger)$2, (LType)$4);
    }
    | '[' INTEGER X type ']'
    {
        $$ = new ArrayType ((long)(BigInteger)$2, (LType)$4);
    }
    ;
    
function_type_args
    : function_type_arg
    {
        $$ = NewList ((LType)$1);
    }
    | function_type_args ',' function_type_arg
    {
        $$ = ListAdd ($1, (LType)$3);
    }
    ;
    
function_type_arg
    : type
    | ELLIPSIS
    {
        $$ = VarArgsType.VarArgs;
    }
    ;
    
function_definition
    : DEFINE return_type GLOBAL_SYMBOL '(' parameter_list ')' function_addr attribute_group_refs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$2, (GlobalSymbol)$3, (List<Parameter>)$5, (List<Block>)$10);
    }
    | DEFINE return_type GLOBAL_SYMBOL '(' parameter_list ')' attribute_group_refs metadata_kvs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$2, (GlobalSymbol)$3, (List<Parameter>)$5, (List<Block>)$10, (SymbolTable<MetaSymbol>)$8);
    }
    | DEFINE return_type GLOBAL_SYMBOL '(' ')' function_addr attribute_group_refs metadata_kvs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$2, (GlobalSymbol)$3, Enumerable.Empty<Parameter> (), (List<Block>)$10, (SymbolTable<MetaSymbol>)$8);
    }
    | DEFINE return_type GLOBAL_SYMBOL '(' ')' attribute_group_refs metadata_kvs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$2, (GlobalSymbol)$3, Enumerable.Empty<Parameter> (), (List<Block>)$9, (SymbolTable<MetaSymbol>)$7);
    }
    | DEFINE return_type GLOBAL_SYMBOL '(' parameter_list ')' function_addr attribute_group_refs metadata_kvs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$2, (GlobalSymbol)$3, (List<Parameter>)$5, (List<Block>)$11, (SymbolTable<MetaSymbol>)$9);
    }
    | DEFINE NOALIAS return_type GLOBAL_SYMBOL '(' parameter_list ')' function_addr attribute_group_refs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$3, (GlobalSymbol)$4, (List<Parameter>)$6, (List<Block>)$11);
    }
    | DEFINE NOALIAS return_type GLOBAL_SYMBOL '(' parameter_list ')' function_addr attribute_group_refs metadata_kvs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$3, (GlobalSymbol)$4, (List<Parameter>)$6, (List<Block>)$12, (SymbolTable<MetaSymbol>)$10);
    }
    | DEFINE linkage calling_convention return_type GLOBAL_SYMBOL '(' parameter_list ')' function_addr attribute_group_refs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$4, (GlobalSymbol)$5, (List<Parameter>)$7, (List<Block>)$12);
    }
    | DEFINE linkage calling_convention return_type GLOBAL_SYMBOL '(' parameter_list ')' function_addr attribute_group_refs metadata_kvs '{' blocks '}'
    {
        $$ = new FunctionDefinition ((LType)$4, (GlobalSymbol)$5, (List<Parameter>)$7, (List<Block>)$13);
    }
    ;
    
function_declaration
    : DECLARE return_type GLOBAL_SYMBOL '(' parameter_list ')' attribute_group_refs
    {
        $$ = new FunctionDeclaration ((LType)$2, (GlobalSymbol)$3, (List<Parameter>)$5);
    }
    | DECLARE NOALIAS return_type GLOBAL_SYMBOL '(' parameter_list ')' attribute_group_refs
    {
        $$ = new FunctionDeclaration ((LType)$3, (GlobalSymbol)$4, (List<Parameter>)$6);
    }
    | DECLARE return_type GLOBAL_SYMBOL '(' parameter_list ')' function_addr attribute_group_refs
    {
        $$ = new FunctionDeclaration ((LType)$2, (GlobalSymbol)$3, (List<Parameter>)$5);
    }
    ;
    
parameter_list
    : parameter
    {
        $$ = NewList ((Parameter)$1);
    }
    | parameter_list ',' parameter
    {
        $$ = ListAdd ($1, (Parameter)$3);
    }
    ;
    
parameter
    : type
    {
        $$ = new Parameter (LocalSymbol.None, (LType)$1);
    }
    | type parameter_attributes
    {
        $$ = new Parameter (LocalSymbol.None, (LType)$1);
    }
    | METADATA
    {
        $$ = new Parameter (LocalSymbol.None, IntegerType.I32);
    }
    | ELLIPSIS
    {
        $$ = new Parameter (LocalSymbol.None, VarArgsType.VarArgs);
    }
    ;
    
parameter_attributes
    : parameter_attribute
    | parameter_attributes parameter_attribute
    {
        $$ = ((ParameterAttributes)$1) | ((ParameterAttributes)$2);
    }
    ;
    
parameter_attribute
    : NONNULL   { $$ = ParameterAttributes.NonNull; }
    | NOCAPTURE { $$ = ParameterAttributes.NoCapture; }
    | READONLY  { $$ = ParameterAttributes.ReadOnly; }
    | WRITEONLY { $$ = ParameterAttributes.WriteOnly; }
    ;
    
function_addr
    : UNNAMED_ADDR
    | LOCAL_UNNAMED_ADDR
    ;
    
attribute_group_refs
    : attribute_group_ref
    | attribute_group_refs attribute_group_ref
    ;
    
attribute_group_ref
    : ATTRIBUTE_GROUP_REF
    ;
    
icmp_condition
    : EQ  { $$ = IcmpCondition.Equal; }
    | NE  { $$ = IcmpCondition.NotEqual; }
    | UGT { $$ = IcmpCondition.UnsignedGreaterThan; }
    | UGE { $$ = IcmpCondition.UnsignedGreaterThanOrEqual; }
    | ULT { $$ = IcmpCondition.UnsignedLessThan; }
    | ULE { $$ = IcmpCondition.UnsignedLessThanOrEqual; }
    | SGT { $$ = IcmpCondition.SignedGreaterThan; }
    | SGE { $$ = IcmpCondition.SignedGreaterThanOrEqual; }
    | SLT { $$ = IcmpCondition.SignedLessThan; }
    | SLE { $$ = IcmpCondition.SignedLessThanOrEqual; }
    ;
    
fcmp_condition
    : TRUE   { $$ = FcmpCondition.True; }
    | FALSE  { $$ = FcmpCondition.False; }
    | ORD    { $$ = FcmpCondition.Ordered; }
    | OEQ    { $$ = FcmpCondition.OrderedEqual; }
    | ONE    { $$ = FcmpCondition.OrderedNotEqual; }
    | OGT    { $$ = FcmpCondition.OrderedGreaterThan; }
    | OGE    { $$ = FcmpCondition.OrderedGreaterThanOrEqual; }
    | OLT    { $$ = FcmpCondition.OrderedLessThan; }
    | OLE    { $$ = FcmpCondition.OrderedLessThanOrEqual; }
    | UNO    { $$ = FcmpCondition.Unordered; }
    | UEQ    { $$ = FcmpCondition.UnorderedEqual; }
    | UNE    { $$ = FcmpCondition.UnorderedNotEqual; }
    | UGT    { $$ = FcmpCondition.UnorderedGreaterThan; }
    | UGE    { $$ = FcmpCondition.UnorderedGreaterThanOrEqual; }
    | ULT    { $$ = FcmpCondition.UnorderedLessThan; }
    | ULE    { $$ = FcmpCondition.UnorderedLessThanOrEqual; }
    ;
    
value
    : constant 
    | LOCAL_SYMBOL  { $$ = new LocalValue ((LocalSymbol)$1); }
    | GLOBAL_SYMBOL { $$ = new GlobalValue ((GlobalSymbol)$1); }
    | INTTOPTR '(' typed_value TO type ')'
    {
        $$ = new IntToPointerValue ((TypedValue)$3, (LType)$5);
    }
    | GETELEMENTPTR INBOUNDS '(' type ',' typed_value ',' element_indices ')'
    {
        $$ = new GetElementPointerValue ((LType)$4, (TypedValue)$6, (List<TypedValue>)$8);
    }
    | BITCAST '(' typed_value TO type ')'
    {
        $$ = new BitcastValue ((TypedValue)$3, (LType)$5);
    }
    ;
    
pointer_value
    : value
    ;

constant
    : NULL            { $$ = NullConstant.Null; }
    | FLOAT_LITERAL   { $$ = new FloatConstant ((double)$1); }
    | INTEGER         { $$ = new IntegerConstant ((BigInteger)$1); }
    | HEX_INTEGER     { $$ = new HexIntegerConstant ((BigInteger)$1); }
    | TRUE            { $$ = BooleanConstant.True; }
    | FALSE           { $$ = BooleanConstant.False; }
    | UNDEF           { $$ = UndefinedConstant.Undefined; }
    | ZEROINITIALIZER { $$ = ZeroConstant.Zero; }
    | CONSTANT_BYTES  { $$ = new BytesConstant ((Symbol)$1); }
    | '<' typed_constants '>'
    {
        $$ = new VectorConstant ((List<TypedConstant>)$2);
    }
    | '[' typed_constants ']'
    {
        $$ = new ArrayConstant ((List<TypedConstant>)$2);
    }
    | '{' typed_values '}'
    {
        $$ = new StructureConstant ((List<TypedValue>)$2);
    }
    ;
    
label_value
    : LABEL LOCAL_SYMBOL
    {
        $$ = new LabelValue ((LocalSymbol)$2);
    }
    ;
    
typed_value
    : type value
    {
        $$ = new TypedValue ((LType)$1, (Value)$2);
    }
    | VOID
    {
        $$ = new TypedValue (VoidType.Void, VoidValue.Void);
    }
    ;
            
typed_pointer_value
    : type pointer_value
    {
        $$ = new TypedValue ((LType)$1, (Value)$2);
    }
    ;
            
typed_values
    : typed_value
    {
        $$ = NewList ((TypedValue)$1);
    }
    | typed_values ',' typed_value
    {
        $$ = ListAdd ($1, (TypedValue)$3);
    }
    ;

typed_constant
    : type constant
    {
        $$ = new TypedConstant ((LType)$1, (Constant)$2);
    }
    ;
    
typed_constants
    : typed_constant
    {
        $$ = NewList ((TypedConstant)$1);
    }
    | typed_constants ',' typed_constant
    {
        $$ = ListAdd ($1, (TypedConstant)$3);
    }
    ;

element_index
    : typed_value
    ;
    
element_indices
    : element_index
    {
        $$ = NewList ((TypedValue)$1);
    }
    | element_indices ',' element_index
    {
        $$ = ListAdd ($1, (TypedValue)$3);
    }
    ;
    
blocks
    : block
    {
        $$ = NewList ((Block)$1);
    }
    | blocks block
    {
        $$ = ListAdd ($1, (Block)$2);
    }
    ;

block
    : assignments terminator_instruction
    {
        $$ = new Block (LocalSymbol.None, (List<Assignment>)$1, (TerminatorInstruction)$2);
    }
    | assignments terminator_instruction metadata_kvs
    {
        $$ = new Block (LocalSymbol.None, (List<Assignment>)$1, (TerminatorInstruction)$2);
    }
    | terminator_instruction
    {
        $$ = new Block (LocalSymbol.None, Enumerable.Empty<Assignment>(), (TerminatorInstruction)$1);
    }
    | terminator_instruction metadata_kvs
    {
        $$ = new Block (LocalSymbol.None, Enumerable.Empty<Assignment>(), (TerminatorInstruction)$1);
    }
    ;
    
assignments
    : assignment
    {
        $$ = NewList ((Assignment)$1);
    }
    | assignments assignment
    {
        $$ = ListAdd ($1, (Assignment)$2);
    }
    ;
    
assignment
    : instruction
    {
        $$ = new Assignment ((Instruction)$1);
    }
    | instruction metadata_kvs
    {
        $$ = new Assignment ((Instruction)$1);
    }
    | LOCAL_SYMBOL '=' instruction
    {
        $$ = new Assignment ((LocalSymbol)$1, (Instruction)$3);
    }
    | LOCAL_SYMBOL '=' instruction metadata_kvs
    {
        $$ = new Assignment ((LocalSymbol)$1, (Instruction)$3);
    }
    ;
    
function_pointer
    : value
    ;
    
function_args
    : function_arg
    {
        $$ = NewList ((Argument)$1);
    }
    | function_args ',' function_arg
    {
        $$ = ListAdd ($1, (Argument)$3);
    }
    ;
    
function_arg
    : type value
    {
        $$ = new Argument ((LType)$1, (Value)$2, (ParameterAttributes)0);
    }
    | type parameter_attributes value
    {
        $$ = new Argument ((LType)$1, (Value)$3, ParameterAttributes.NonNull);
    }
    | METADATA type LOCAL_SYMBOL
    {
        $$ = new Argument ((LType)$2, new LocalValue ((LocalSymbol)$3), (ParameterAttributes)0);
    }
    | METADATA type constant
    {
        $$ = new Argument ((LType)$2, (Value)$3, (ParameterAttributes)0);
    }
    | METADATA META_SYMBOL
    {
        $$ = new Argument (IntegerType.I32, new MetaValue ((MetaSymbol)$2), (ParameterAttributes)0);
    }
    | METADATA META_SYMBOL '(' ')'
    {
        $$ = new Argument (IntegerType.I32, new MetaValue ((MetaSymbol)$2), (ParameterAttributes)0);
    }
    | METADATA META_SYMBOL '(' metadata_value_args ')'
    {
        $$ = new Argument (IntegerType.I32, new MetaValue ((MetaSymbol)$2), (ParameterAttributes)0);
    }
    ;
    
metadata_value_args
    : metadata_value_arg
    | metadata_value_args ',' metadata_value_arg
    ;
    
metadata_value_arg
    : constant
    | SYMBOL
    ;
    
phi_vals
    : phi_val
    {
        $$ = NewList ((PhiValue)$1);
    }
    | phi_vals ',' phi_val
    {
        $$ = ListAdd ($1, (PhiValue)$3);
    };
    
phi_val
    : '[' value ',' value ']'
    {
        $$ = new PhiValue ((Value)$2, (Value)$4);
    }
    ;
    
switch_cases
    : switch_case
    {
        $$ = NewList ((SwitchCase)$1);
    }
    | switch_cases switch_case
    {
        $$ = ListAdd ($1, (SwitchCase)$2);
    }
    ;
    
switch_case
    : typed_constant ',' label_value
    {
        $$ = new SwitchCase ((TypedConstant)$1, (LabelValue)$3);
    }
    ;
    
wrappings
    : wrapping
    | wrappings wrapping
    ;
    
wrapping
    : NUW
    | NSW
    ;
    
calling_convention
    : FASTCC
    ;
    
terminator_instruction
    : BR label_value
    {
        $$ = new UnconditionalBrInstruction ((LabelValue)$2);
    }
    | BR I1 value ',' label_value ',' label_value
    {
        $$ = new ConditionalBrInstruction ((Value)$3, (LabelValue)$5, (LabelValue)$7);
    }
    | RET typed_value
    {
        $$ = new RetInstruction ((TypedValue)$2);
    }
    | SWITCH typed_value ',' label_value '[' switch_cases ']'
    {
        $$ = new SwitchInstruction ((TypedValue)$2, (LabelValue)$4, (List<SwitchCase>)$6);
    }
    ;
    
instruction
    : ADD type value ',' value
    {
        $$ = new AddInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | ADD wrappings type value ',' value
    {
        $$ = new AddInstruction ((LType)$3, (Value)$4, (Value)$6);
    }
    | ALLOCA type ',' ALIGN INTEGER
    {
        $$ = new AllocaInstruction ((LType)$2, (int)(BigInteger)$5);
    }
    | AND type value ',' value
    {
        $$ = new AndInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | BITCAST typed_value TO type
    {
        $$ = new BitcastInstruction ((TypedValue)$2, (LType)$4);
    }
    | CALL return_type function_pointer '(' function_args ')'
    {
        $$ = new CallInstruction ((LType)$2, (Value)$3, (List<Argument>)$5, false);
    }
    | CALL calling_convention return_type function_pointer '(' function_args ')'
    {
        $$ = new CallInstruction ((LType)$3, (Value)$4, (List<Argument>)$6, false);
    }
    | CALL return_type function_pointer '(' function_args ')' attribute_group_refs
    {
        $$ = new CallInstruction ((LType)$2, (Value)$3, (List<Argument>)$5, false);
    }
    | TAIL CALL return_type function_pointer '(' function_args ')' attribute_group_refs
    {
        $$ = new CallInstruction ((LType)$3, (Value)$4, (List<Argument>)$6, true);
    }
    | TAIL CALL return_type function_pointer '(' function_args ')'
    {
        $$ = new CallInstruction ((LType)$3, (Value)$4, (List<Argument>)$6, true);
    }
    | TAIL CALL calling_convention return_type function_pointer '(' function_args ')'
    {
        $$ = new CallInstruction ((LType)$4, (Value)$5, (List<Argument>)$7, true);
    }
    | EXTRACTELEMENT typed_value ',' typed_value
    {
        $$ = new ExtractElementInstruction ((TypedValue)$2, (TypedValue)$4);
    }
    | FADD type value ',' value
    {
        $$ = new FaddInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | FCMP fcmp_condition type value ',' value
    {
        $$ = new FcmpInstruction ((FcmpCondition)$2, (LType)$3, (Value)$4, (Value)$6);
    }
    | FDIV type value ',' value
    {
        $$ = new FdivInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | FMUL type value ',' value
    {
        $$ = new FmulInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | FPTOUI typed_value TO type
    {
        $$ = new FptouiInstruction ((TypedValue)$2, (LType)$4);
    }
    | FPTOSI typed_value TO type
    {
        $$ = new FptosiInstruction ((TypedValue)$2, (LType)$4);
    }
    | FSUB type value ',' value
    {
        $$ = new FsubInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | GETELEMENTPTR type ',' typed_value ',' element_indices
    {
        $$ = new GetElementPointerInstruction ((LType)$2, (TypedValue)$4, (List<TypedValue>)$6);
    }
    | GETELEMENTPTR INBOUNDS type ',' typed_value ',' element_indices
    {
        $$ = new GetElementPointerInstruction ((LType)$3, (TypedValue)$5, (List<TypedValue>)$7);
    }
    | ICMP icmp_condition type value ',' value
    {
        $$ = new IcmpInstruction ((IcmpCondition)$2, (LType)$3, (Value)$4, (Value)$6);
    }
    | INSERTELEMENT typed_value ',' typed_value ',' typed_value
    {
        $$ = new InsertElementInstruction ((TypedValue)$2, (TypedValue)$4, (TypedValue)$6);
    }
    | LOAD type ',' typed_pointer_value ',' ALIGN INTEGER
    {
        $$ = new LoadInstruction ((LType)$2, (TypedValue)$4);
    }
    | LSHR type value ',' value
    {
        $$ = new LshrInstruction ((LType)$2, (Value)$3, (Value)$5, false);
    }
    | LSHR EXACT type value ',' value
    {
        $$ = new LshrInstruction ((LType)$3, (Value)$4, (Value)$6, true);
    }
    | OR type value ',' value
    {
        $$ = new OrInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | MUL type value ',' value
    {
        $$ = new MultiplyInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | MUL wrappings type value ',' value
    {
        $$ = new MultiplyInstruction ((LType)$3, (Value)$4, (Value)$6);
    }
    | PHI type phi_vals
    {
        $$ = new PhiInstruction ((LType)$2, (List<PhiValue>)$3);
    }
    | PTRTOINT typed_value TO type
    {
        $$ = new PtrtointInstruction ((TypedValue)$2, (LType)$4);
    }
    | SDIV type value ',' value
    {
        $$ = new SdivInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | SELECT type value ',' typed_value ',' typed_value
    {
        $$ = new SelectInstruction ((LType)$2, (Value)$3, (TypedValue)$5, (TypedValue)$7);
    }
    | SEXT typed_value TO type
    {
        $$ = new SextInstruction ((TypedValue)$2, (LType)$4);
    }
    | SHL type value ',' value
    {
        $$ = new ShlInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | SHL wrappings type value ',' value
    {
        $$ = new ShlInstruction ((LType)$3, (Value)$4, (Value)$6);
    }
    | SHUFFLEVECTOR typed_value ',' typed_value ',' typed_value
    {
        $$ = new ShuffleVectorInstruction ((TypedValue)$2, (TypedValue)$4, (TypedValue)$6);
    }
    | SITOFP typed_value TO type
    {
        $$ = new SitofpInstruction ((TypedValue)$2, (LType)$4);
    }
    | STORE typed_value ',' typed_pointer_value ',' ALIGN INTEGER
    {
        $$ = new StoreInstruction (value: (TypedValue)$2, pointer: (TypedValue)$4);
    }
    | SUB type value ',' value
    {
        $$ = new SubInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | SUB wrappings type value ',' value
    {
        $$ = new SubInstruction ((LType)$3, (Value)$4, (Value)$6);
    }
    | TRUNC typed_value TO type
    {
        $$ = new TruncInstruction ((TypedValue)$2, (LType)$4);
    }
    | UDIV type value ',' value
    {
        $$ = new UdivInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | UITOFP typed_value TO type
    {
        $$ = new UitofpInstruction ((TypedValue)$2, (LType)$4);
    }
    | UREM type value ',' value
    {
        $$ = new UremInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | XOR type value ',' value
    {
        $$ = new XorInstruction ((LType)$2, (Value)$3, (Value)$5);
    }
    | ZEXT typed_value TO type
    {
        $$ = new ZextInstruction ((TypedValue)$2, (LType)$4);
    }
    ;
    
%%

}

