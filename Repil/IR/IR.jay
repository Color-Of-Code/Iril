%{
using System;
using System.Collections.Generic;
using System.IO;
using System.Numerics;
using System.Text;

using Repil.Types;

#pragma warning disable 219,414

namespace Repil.IR
{
	public partial class Parser
	{
%}

%token INTEGER FLOAT_LITERAL STRING TRUE FALSE UNDEF VOID NULL NONNULL LABEL X
%token SOURCE_FILENAME TARGET DATALAYOUT TRIPLE
%token GLOBAL_SYMBOL LOCAL_SYMBOL META_SYMBOL
%token TYPE HALF FLOAT DOUBLE I1 I8 I16 I32 I64
%token DEFINE UNNAMED_ADDR LOCAL_UNNAMED_ADDR
%token ATTRIBUTE_GROUP_REF
%token RET BR SWITCH INDIRECTBR INVOKE RESUME CATCHSWITCH CATCHRET CLEANUPRET UNREACHABLE
%token FNEG
%token ADD FADD SUB FSUB MUL FMUL UDIV SDIV FDIV UREM SREM FREM
%token SHL LSHR ASHR
%token AND OR XOR
%token EXTRACTELEMENT INSERTELEMENT SHUFFLEVECTOR
%token EXTRACTVALUE INSERTVALUE
%token ALLOCA LOAD STORE FENCE CMPXCHG ATOMICRMW GETELEMENTPTR ALIGN INBOUNDS INRANGE
%token TRUNC ZEXT SEXT FPTRUNC FPEXT TO
%token FPTOUI FPTOSI UITOFP SITOFP PTRTOINT INTTOPTR BITCAST ADDRSPACECAST
%token ICMP EQ NE UGT UGE ULT ULE SGT SGE SLT SLE
%token FCMP OEQ OGT OGE OLT OLE ONE ORD UEQ UNE UNO
%token PHI SELECT CALL VA_ARG
%token LANDINGPAD CATCHPAD CLEANUPPAD

%start module
%%

module
	: module_parts
	;
	
module_parts
    : module_part
    | module_parts module_part
    ;
    
module_part
    : SOURCE_FILENAME '=' STRING
    {
        module.SourceFilename = (string)$3;
    }
    | TARGET DATALAYOUT '=' STRING
    {
        module.TargetDatalayout = (string)$4;
    }
    | TARGET TRIPLE '=' STRING
    {
        module.TargetTriple = (string)$4;
    }
    | LOCAL_SYMBOL '=' TYPE literal_structure
    {
        module.IdentifiedStructures[(Symbol)$1] = (StructureType)$4;
    }
    | function_definition
    ;
    
literal_structure
    : '{' type_list '}'
    {
        var s = new LiteralStructureType ();
        $$ = s;
    }
    ;
    
type_list
    : type
    {
        $$ = NewList ((LType)$1);
    }
    | type_list ',' type
    {
        $$ = ListAdd ($1, (LType)$3);
    }
    ;
    
type
    : literal_structure
    | VOID   { $$ = VoidType.Void; }
    | HALF   { $$ = FloatType.Half; }
    | FLOAT  { $$ = FloatType.Float; }
    | DOUBLE { $$ = FloatType.Double; }
    | I1     { $$ = IntegerType.I1; }
    | I8     { $$ = IntegerType.I8; }
    | I16    { $$ = IntegerType.I8; }
    | I32    { $$ = IntegerType.I8; }
    | I64    { $$ = IntegerType.I8; }
    | type '(' type_list ')'
    {
        $$ = new FunctionType ();
    }
    | type '*'
    {
        $$ = new PointerType ((LType)$1, 0);
    }
    | LOCAL_SYMBOL
    {
        $$ = new NamedType ((Symbol)$1);
    }
    | '<' INTEGER X type '>'
    {
        $$ = new VectorType ((int)(BigInteger)$2, (LType)$4);
    }
    ;
    
function_definition
    : DEFINE type GLOBAL_SYMBOL '(' parameter_list ')' function_addr attribute_group_refs '{' assignments '}'
    {
        var f = new FunctionDefinition ((GlobalSymbol)$3, (LType)$2, (List<Parameter>)$5, (List<Assignment>)$10);
    }
    ;
    
parameter_list
    : parameter
    | parameter_list parameter
    ;
    
parameter
    : type
    ;
    
function_addr
    : UNNAMED_ADDR
    | LOCAL_UNNAMED_ADDR
    ;
    
attribute_group_refs
    : attribute_group_ref
    | attribute_group_refs attribute_group_ref
    ;
    
attribute_group_ref
    : ATTRIBUTE_GROUP_REF
    ;
    
icmp_condition
    : EQ  { $$ = IcmpCondition.Equal; }
    | NE  { $$ = IcmpCondition.NotEqual; }
    | UGT { $$ = IcmpCondition.UnsignedGreaterThan; }
    | UGE { $$ = IcmpCondition.UnsignedGreaterThanOrEqual; }
    | ULT { $$ = IcmpCondition.UnsignedLessThan; }
    | ULE { $$ = IcmpCondition.UnsignedLessThanOrEqual; }
    | SGT { $$ = IcmpCondition.SignedGreaterThan; }
    | SGE { $$ = IcmpCondition.SignedGreaterThanOrEqual; }
    | SLT { $$ = IcmpCondition.SignedLessThan; }
    | SLE { $$ = IcmpCondition.SignedLessThanOrEqual; }
    ;
    
value
    : constant 
    | LOCAL_SYMBOL  { $$ = new LocalValue ((LocalSymbol)$1); }
    | GLOBAL_SYMBOL { $$ = new GlobalValue ((GlobalSymbol)$1); }
    ;

constant
    : NULL            { $$ = NullConstant.Null; }
    | FLOAT_LITERAL   { $$ = new FloatConstant ((double)$1); }
    | INTEGER         { $$ = new IntegerConstant ((BigInteger)$1); }
    | TRUE            { $$ = BooleanConstant.True; }
    | FALSE           { $$ = BooleanConstant.False; }
    | '<' typed_constants '>'
    {
        $$ = new VectorConstant ((List<TypedConstant>)$2);
    }
    ;
    
label_value
    : LABEL LOCAL_SYMBOL
    {
        $$ = new LabelValue ((LocalSymbol)$2);
    }
    ;
    
typed_value
    : type value
    {
        $$ = new TypedValue ((LType)$1, (Value)$2);
    }
    ;
            
typed_constant
    : type constant
    {
        $$ = new TypedConstant ((LType)$1, (Constant)$2);
    }
    ;
    
typed_constants
    : typed_constant
    {
        $$ = NewList ((TypedConstant)$1);
    }
    | typed_constants ',' typed_constant
    {
        $$ = ListAdd ($1, (TypedConstant)$3);
    }
    ;

tbaa
    : META_SYMBOL META_SYMBOL
    ;
    
element_index
    : type INTEGER
    {
        $$ = (int)(BigInteger)$2;
    }
    ;
    
element_indices
    : element_index
    {
        $$ = NewList ((int)$1);
    }
    | element_indices ',' element_index
    {
        $$ = ListAdd ($1, (int)$3);
    }
    ;

assignments
    : assignment
    {
        $$ = NewList ((Assignment)$1);
    }
    | assignments assignment
    {
        $$ = ListAdd ($1, (Assignment)$2);
    }
    ;
    
assignment
    : instruction
    {
        $$ = new Assignment ((Instruction)$1);
    }
    | LOCAL_SYMBOL '=' instruction
    {
        $$ = new Assignment ((LocalSymbol)$1, (Instruction)$3);
    }
    ;
    
function_pointer
    : value
    ;
    
function_args
    : function_arg
    {
        $$ = NewList ((Argument)$1);
    }
    | function_args ',' function_arg
    {
        $$ = ListAdd ($1, (Argument)$3);
    }
    ;
    
function_arg
    : type value
    {
        $$ = new Argument ((LType)$1, (Value)$2, (ParameterAttributes)0);
    }
    | type NONNULL value
    {
        $$ = new Argument ((LType)$1, (Value)$3, ParameterAttributes.NonNull);
    }
    ;
    
instruction
    : RET
    | BR I1 value ',' label_value ',' label_value
    {
        $$ = new ConditionalBrInstruction ((Value)$3, (LabelValue)$5, (LabelValue)$7);
    }
    | BR label_value
    {
        $$ = new UnconditionalBrInstruction ((LabelValue)$2);
    }
    | STORE typed_value ',' typed_value ',' ALIGN INTEGER ',' tbaa
    {
        $$ = new StoreInstruction ((TypedValue)$2, (TypedValue)$4);
    }
    | ICMP icmp_condition type value ',' value
    {
        $$ = new IcmpInstruction ((IcmpCondition)$2, (LType)$3, (Value)$4, (Value)$6);
    }
    | BITCAST typed_value TO type
    {
        $$ = new BitcastInstruction ((TypedValue)$2, (LType)$4);
    }
    | GETELEMENTPTR INBOUNDS type ',' typed_value ',' element_indices
    {
        $$ = new GetElementPointerInstruction ((LType)$3, (TypedValue)$5, (List<int>)$7);
    }
    | CALL type function_pointer '(' function_args ')'
    {
        $$ = new CallInstruction ((LType)$2, (Value)$3, (List<Argument>)$5);
    }
    ;
    
%%

}

